thing: sphere : hittable {
    center: vec3,
    radius: num,
    mat: material,
}

$sphere (center: vec3, radius: num, material: material) {
    my.center = center;
    my.radius = radius;
    my.mat = material;
}

@hit: num (r: ray, ray_t: interval, rec: hit_record) -> sphere {
    vec3 oc = r.origin() - my.center;
    num a = (r.direction()).length_squared();
    num half_b = dot(oc, r.direction());
    num c = oc.length_squared() - my.radius * my.radius;

    num discriminant = half_b * half_b - a * c;
    if (discriminant < 0) ret false;
    num sqrtd = sqrt(discriminant);

    ## Find the nearest root that lies in the acceptable range.
    num root = (-half_b - sqrtd) / a;
    if (!ray_t.surrounds(root)) {
        root = (-half_b + sqrtd) / a;
        if (!ray_t.surrounds(root))
            ret false;
    }

    rec.t = root;
    rec.p = r.at(rec.t);
    vec3 outward_normal = (rec.p - my.center) / my.radius;
    rec.set_face_normal(r, outward_normal);
    rec.mat = my.mat;

    ret true;
}
